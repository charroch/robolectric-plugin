import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'groovy'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6
group = 'com.novoda.gradle'
version = '0.0.1-SNAPSHOT'
ext.packaging = 'jar'

def isDevBuild
def isCiBuild
def isReleaseBuild

def sonatypeRepositoryUrl

if (hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
}

artifacts {
    archives jar
}


repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'com.android.tools.build:gradle:0.4.2'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle robolectric plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

idea.project {
    jdkName = '1.6'

    ipr.withXml { provider ->
        def node = provider.asNode()
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        } else {
            mavenDeployer {
                if (isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }
                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                project {
                    name 'Gradle Robolectric plugin'
                    group = 'com.novoda.gradle'
                    artifactId = 'robolectric-plugin'

                    description 'Gradle plugin that provides test task using robolectric to overcome local unit test run'
                    url 'https://github.com/novoda/robolectric-plugin'
                    inceptionYear '2013'

                    scm {
                        url 'https://github.com/novoda/robolectric-plugin'
                        connection 'scm:https://git@github.com/novoda/robolectric-plugin.git'
                        developerConnection 'scm:git://github.com/novoda/robolectric-plugin.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'charroch'
                            name 'Carl-Gustaf Harroch'
                            email 'carl@novoda.com'
                        }
                    }
                }
            }
        }
    }
}
